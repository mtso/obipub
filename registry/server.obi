http := mod("./http.obi");
db := mod("./db.obi");
json := mod("../lib/json.obi");

index := mod("./views/index.obi");
not_found := mod("./views/not_found.obi");
packageView := mod("./views/package.obi");

server := http.HttpServer();

// FIXME(mtso): make this an environment variable!
DATA_DIR := process_env("DATA_DIR") or "/mnt/volume_sfo3_01";
PORT := 8080;

match (file_exists(DATA_DIR)) {
    nil -> {
        print("Expect DATA_DIR to point to a valid filepath. DATA_DIR=" + str(DATA_DIR));
        process_exit(1);
    }
    _ -> ();
};

packageDb := db.makePackageDb(DATA_DIR);

server.addRoute("/assets/packages/:filename") fun(request, respond) {
    filename := http.makeRelative(request.params.filename);
    match (file := readfile_bytes(DATA_DIR + "/packages/" + filename)) {
        nil -> return respond(404, [Content-Type = "application/json"], text_encode("{\"error\":\"not_found\"}"));
        _ -> ();
    };
    headers := [
        Content-Type = "application/tar",
    ];
    respond(200, headers, file);
};

server.addRoute("/api/packages/:name/versions/:version/archive.tar.gz") fun(request, respond) {
    name := request.params.name;
    version := request.params.version;
    filename := http.makeRelative(name + "-" + version + ".tar.gz");
    package := readfile_bytes("./packages/" + filename);
    headers := [
        Content-Type = "text/plain; charset=utf-8",
        Location = "/assets/packages/" + filename,
        x-frame-options = "SAMEORIGIN",
    ];
    respond(303, headers, array_make(0));
};

server.addRoute("/api/packages/:name") fun(request, respond) {
    result := packageDb.listPackage(request.params.name);
    headers := [];
    headers.("Content-Type") = "application/json";

    match result.ok {
        false -> match result.error {
            "not_found" -> return respond(404, headers, text_encode("{\"error\":\"not_found\"}"));
            "internal_error" -> return respond(500, headers, text_encode("{\"error\":\"internal_error\"}"));
            _ -> {
                print("Unhandled error: " + str(result.error));
                return respond(500, headers, text_encode("{\"error\":\"internal_error\"}"));
            }
        };
        _ -> ();
    };

    info := result.value;
    respond(200, headers, text_encode(json.stringify_pretty(info, 2)));
};

fun filter(list, fn) {
    newlist := [];
    newlistIndex := 0;
    i := 0;
    listlen := len(list);
    while() { i < listlen; } {
        match (fn(list.(i))) {
            true -> {
                newlist.(newlistIndex) = list.(i);
                newlistIndex = newlistIndex + 1;
            }
            false -> ();
        };
        i = i + 1;
    };
    return newlist;
}
fun map(list, fn) {
    newlist := [];
    i := 0;
    listlen := len(list);
    while() { i < listlen; } {
        newlist.(i) = fn(list.(i));
        i = i + 1;
    };
    return newlist;
}

server.addRoute("/packages/:name") fun(req, respond) {
    res := packageDb.listPackage(req.params.name);
    match res.ok {
        false -> match res.error {
            "not_found" -> return respond(404, [ Content-Type = "text/html" ], text_encode(not_found.getMarkup()));
            _ -> {
                print("Unhandled error: " + res.error);
                return respond(404, [ Content-Type = "text/html" ], text_encode(not_found.getMarkup()));
            }
        };
        _ -> ();
    };

    markup := packageView.getMarkup(res.value);
    respond(200, [Content-Type = "text/html"], text_encode(markup));
};

server.addRoute("/") fun(req, res) {
    packageNames := readdir(DATA_DIR + "/index");
    files := filter(packageNames) fun(info) { info.isFile; };
    names := map(files) fun(info) { http.replaceAll(info.name, ".json", ""); };
    markup := index.getMarkup(list_sort(names));
    res(200, [Content-Type = "text/html"], text_encode(markup));
};

server.addRoute("/style.css") fun(req, res) {
    style := readfile_bytes("./static/style.css") or readfile_bytes("./registry/static/style.css");
    match style {
        nil -> res(404, [Content-Type = "text/plain"], text_encode("Not found."));
        _ -> res(200, [Content-Type = "text/css"], style);
    };
};

server.setDefaultHandler() fun(request, respond) {
    headers := [];
    headers.("Content-Type") = "application/json";
    respond(404, headers, text_encode("{\"error\":\"not_found\"}"));
};

print("Listening on " + str(PORT));
server.listen(PORT);
